10 REM MSX-BASIC GAME EXTENSION LOADER
20 REM CHECK IF ALREADY LOADED
30 DEFINT A-Z
40 F=0:V=0:M=0
50 ON ERROR GOTO 80
60 _ARTINFO(V,F,M)
70 GOTO 100
80 RESUME 90:ON ERROR GOTO 0
90 BLOAD "ARTISANE.bin",R
100 _ARTINFO(V,F,M)
110 PRINT "ARTISAN BASIC v";
120 C=(V AND &HF0)/16:IF C>0 THEN PRINT CHR$(C+48);
130 C=V AND &HF:PRINT CHR$(C+48);".";
140 IF V>=0 THEN C=(V AND &HF000)/4096 ELSE C=(CDBL(V AND &HF000)+65536)/4096
150 PRINT CHR$(C+48);
160 C=(V AND &H0F00)/256:IF C>0 THEN PRINT CHR$(C+48) ELSE PRINT
170 PRINT "Page 0 free bytes = 16128"
180 PRINT "Page 1 free bytes =";32768-M
190 PRINT "BASIC extension "; 
200 IF (F AND 1024) <> 0 THEN PRINT "enabled" ELSE PRINT "disabled"
210 PRINT "DEFUSR entry "; 
220 IF (F AND 2048) <> 0 THEN PRINT "enabled" ELSE PRINT "disabled"
230 PRINT "Available command sets: "
240 IF (F AND 1) <> 0 THEN PRINT "SOUND,";
250 IF (F AND 2) <> 0 THEN PRINT "RAM,";
260 IF (F AND 4) <> 0 THEN PRINT "VRAM,";
270 IF (F AND 8) <> 0 THEN PRINT "BLIT,";
280 IF (F AND 16) <> 0 THEN PRINT "SPRITE,";
290 IF (F AND 32) <> 0 THEN PRINT "GENCAL,";
300 IF (F AND 64) <> 0 THEN PRINT "TILE,";
310 IF (F AND 128) <> 0 THEN PRINT "BOX,";
320 IF (F AND 256) <> 0 THEN PRINT "ANIM,";
330 IF (F AND 512) <> 0 THEN PRINT "COLL,";
340 IF (F AND 4096) <> 0 THEN PRINT "DECOMP";
350 PRINT

